plugins {
    id("org.jetbrains.kotlin.jvm")
    id("org.jetbrains.kotlin.kapt")
    id("org.jetbrains.kotlin.plugin.allopen")
    id("io.micronaut.library")
    id("maven-publish")
    id("signing")
}

group "info.novatec"

micronaut {
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("micronaut.camunda.external.client.feature.*")
    }
}

dependencies {
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")

    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut.serde:micronaut-serde-jackson")

    api("org.camunda.bpm:camunda-external-task-client:$camundaVersion")
    api("com.sun.xml.bind:jaxb-impl:$jaxbImpl")

    runtimeOnly("ch.qos.logback:logback-classic")

    kaptTest platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    kapt("io.micronaut:micronaut-inject-java:$micronautVersion")

    testImplementation("io.micronaut.test:micronaut-test-junit5")
    testImplementation("org.assertj:assertj-core")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("io.micronaut:micronaut-http-server-netty")
    testRuntimeOnly("org.yaml:snakeyaml")
    testRuntimeOnly("jakarta.xml.bind:jakarta.xml.bind-api:$jakartaXmlBind")
}

java {
    sourceCompatibility = JavaVersion.toVersion(javaLanguageVersion)
    targetCompatibility = JavaVersion.toVersion(javaLanguageVersion)
}

java {
    withJavadocJar()
    withSourcesJar()
}
tasks {
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = javaLanguageVersion
        }
    }
}

// based on https://docs.gradle.org/current/userguide/publishing_maven.html#sec:modifying_the_generated_pom and https://central.sonatype.org/pages/requirements.html#sufficient-metadata
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'micronaut-camunda-external-client-feature'
                description = 'Camunda External Client for Micronaut'
                url = 'https://github.com/camunda-community-hub/micronaut-camunda-external-client'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'The micronaut-camunda Team'
                        organization = 'Camunda Community'
                        organizationUrl = 'https://github.com/camunda-community-hub'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/camunda-community-hub/micronaut-camunda-external-client.git'
                    developerConnection = 'scm:git:https://github.com/camunda-community-hub/micronaut-camunda-external-client.git'
                    url = 'https://github.com/camunda-community-hub/micronaut-camunda-external-client'
                }
            }
        }
    }
    repositories {
        // Release deployments via OSSRH Staging API compatibility layer
        maven {
            name = "ossrh-staging-api"
            url = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("CENTRAL_TOKEN_USER")
                password = System.getenv("CENTRAL_TOKEN_PWD")
            }
        }
    }
}

// see https://docs.gradle.org/current/userguide/signing_plugin.html#sec:specifying_what_to_sign and https://central.sonatype.org/pages/working-with-pgp-signatures.html#generating-a-key-pair
signing {
    sign publishing.publications.mavenJava
}
